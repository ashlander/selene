if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU" OR "${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang")

    list(APPEND SELENE_COMPILE_OPTIONS -Wall -Wextra -Wpedantic)

    if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU" AND CMAKE_CXX_COMPILER_VERSION GREATER_EQUAL 8)
        # GCC 7 emits some spurious warning with -Wconversion enabled, so we don't enable it then.
        list(APPEND SELENE_COMPILE_OPTIONS -Wconversion)
    endif()

    if (SELENE_WARNINGS_AS_ERRORS AND NOT SELENE_ENABLE_SANITIZERS)
        list(APPEND SELENE_COMPILE_OPTIONS -Werror)
    endif()

    if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
        list(APPEND SELENE_IMG_COMPILE_OPTIONS -Wno-clobbered)
    endif()

elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")

    list(APPEND SELENE_COMPILE_OPTIONS /MP /permissive- /wd4324 /wd4611)

    if(NOT "${CMAKE_GENERATOR}" MATCHES "NMake")
        string(REGEX REPLACE " /W[0-4]" "" CMAKE_C_FLAGS "${CMAKE_C_FLAGS}")
        string(REGEX REPLACE " /W[0-4]" "" CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
        list(APPEND SELENE_COMPILE_OPTIONS /W4)
    endif()

    if(SELENE_WARNINGS_AS_ERRORS)
        list(APPEND SELENE_COMPILE_OPTIONS /WX)
    endif()

    list(APPEND SELENE_COMPILE_DEFINITIONS _CRT_SECURE_NO_WARNINGS _SCL_SECURE_NO_WARNINGS BOOST_CONFIG_SUPPRESS_OUTDATED_MESSAGE)

endif()
